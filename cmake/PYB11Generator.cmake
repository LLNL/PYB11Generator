#-----------------------------------------------------------------------------------
# Based on Mike Davis' original Cmake scripts for handling PYB11Generator extensions
# in Spheral.
#
# PYB11_GENERATE_BINDINGS
#     - Generates the Python bindings for each module in the list
#     - Generates python stamp files for listing python dependency file to help
#       detecting changes in the pyb11 python files at build time
#
# Variables that must be set before calling PYB11_GENERATE_BINDINGS:
#   PYB11_MODULE_NAME
#     - Pyb11 module to be generated
#   PYTHON_EXE
#     - Python executable
#   <PYB11_MODULE_NAME>_DEPENDS
#     - Any target dependencies that must be built before generating the module
#
# To get the names of the generated source
# use: ${PYB11_GENERATED_SOURCE}
#-----------------------------------------------------------------------------------

macro(PYB11_GENERATE_BINDINGS PYB11_MODULE_NAME)
  set(PYB11_SOURCE "${PYB11_MODULE_NAME}_PYB11.py")
  set(PYB11_GENERATED_SOURCE "${PYB11_MODULE_NAME}.cc")

  # Place we need in the Python path
  set(PYTHON_ENV 
      ${PYB11GENERATOR_ROOT_DIR}
      ${EXTRA_PYB11_ENV_VARS})

  # Format list into a one line shell friendly format
  STRING(REPLACE ";" "<->" PYTHON_ENV_STR ${PYTHON_ENV})
  string(APPEND PYTHON_ENV_STR ":${CMAKE_CURRENT_SOURCE_DIR}")

  # Generating python stamp files to detect changes in PYB11_SOURCE and
  # its included modules
  if(EXISTS ${PYTHON_EXE})
    # Python must exist to generate at config time
    if(NOT EXISTS "${CMAKE_CURRENT_BINARY_DIR}/${PYB11_MODULE_NAME}_stamp.cmake")
      # Generate stamp files at config time
      execute_process(COMMAND env PYTHONPATH=\"${PYTHON_ENV_STR}\"
                      ${PYTHON_EXE} ${PYB11GENERATOR_ROOT_DIR}/cmake/moduleCheck.py 
                      ${PYB11_MODULE_NAME}
                      ${CMAKE_CURRENT_SOURCE_DIR}/${PYB11_SOURCE}
                      WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                      )
    endif()

    # Include list of dependent python files
    include(${CMAKE_CURRENT_BINARY_DIR}/${PYB11_MODULE_NAME}_stamp.cmake)
  endif()

  # Always regenerate the stamp files at build time. Any change in the stamp file
  # will trigger a rebuild of the target pyb11 module
  add_custom_target(${PYB11_MODULE_NAME}_stamp ALL
                    COMMAND env PYTHONPATH=\"${PYTHON_ENV_STR}\"
                    ${PYTHON_EXE} ${PYB11GENERATOR_ROOT_DIR}/cmake/moduleCheck.py
                    ${PYB11_MODULE_NAME}
                    ${CMAKE_CURRENT_SOURCE_DIR}/${PYB11_SOURCE}
                    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                    )

  # Generate the actual pyb11 module cpp source file
  add_custom_command(OUTPUT ${PYB11_GENERATED_SOURCE}
                     COMMAND env PYTHONPATH=\"${PYTHON_ENV_STR}\"
                     ${PYTHON_EXE} -c
                     'from PYB11Generator import * \; 
                     import ${PYB11_MODULE_NAME}_PYB11 \;
                     PYB11generateModule(${PYB11_MODULE_NAME}_PYB11, \"${PYB11_MODULE_NAME}\") '
                     DEPENDS ${PYB11_MODULE_NAME}_stamp ${${PYB11_MODULE_NAME}_DEPENDS} ${PYB11_SOURCE}
                     )

endmacro()

#-----------------------------------------------------------------------------------
# PYB11Generator_add_module
#     - Add a module with the pybind11 source generated by PYB11Generator
#       Uses the pybind11_add_modele method, just doing our initial generation first
#
# package_name : *name* of package to make into a library
#
# Variables that must be set before calling PYB11Generator_add_module:
#     <package_name>_ADDITIONAL_INCLUDES
#         - List of addition includes needed by a given package
#     <package_name>_ADDITIONAL_SOURCE
#         - List of additional sources to build library with
#     <package_name>_CXX_LIBS
#         - List of link libraries that are required
#     <package_name>_depends
#         - List of targets the library depends on
#     <package_name>_INSTALL_PATH
#         - Path to install extension module -- defaults to ${Python_SITEARCH}/${package_name}
#-----------------------------------------------------------------------------------

function(PYB11Generator_add_module package_name)

  # Generate the pybind11 C++ source file
  PYB11_GENERATE_BINDINGS(${package_name})

  # Now the normal pybind11 build can proceed
  include_directories(${CMAKE_CURRENT_SOURCE_DIR} ${${package_name}_ADDITIONAL_DEPENDS})
  pybind11_add_module(${package_name} ${package_name}.cc)
  set_target_properties(${package_name} PROPERTIES SUFFIX ".so")

  # Installation
  if (NOT ${package_name}_INSTALL_PATH)
    set(${package_name}_INSTALL_PATH ${Python3_SITEARCH}/${package_name})
  endif()
  install(TARGETS ${package_name} DESTINATION ${${package_name}_INSTALL_PATH})

endfunction()
